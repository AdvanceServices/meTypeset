*** Settings ***
Documentation     This series of test cases evaluates the basic environment setup and the ability to parse basic documents.
Library           Process
Library           OperatingSystem
Library           XML
Library           Collections

*** Test Cases ***
000 Environment
    ${result} =    Run Process    python ../bin/meTypeset.py docx 001.docx ./000 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    Should Not Contain    ${result.stderr}    cannot find or open
    Should Not Contain    ${result.stdout}    ERROR
    [Teardown]    Remove Directory    000    recursive=True

001 Basic paragraph
    ${result} =    Run Process    python ../bin/meTypeset.py docx 001.docx ./001 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./001/nlm/out.xml
    ${paragraph}=    Get Element    ${xml}    body/sec/p
    Elements Should Match    ${paragraph}    <p>The most basic document possible.</p>
    [Teardown]    Remove Directory    001    recursive=True

002 Basic paragraph with italics
    [Tags]    italic    002    formatting
    ${result} =    Run Process    python ../bin/meTypeset.py docx 002.docx ./002 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./002/nlm/out.xml
    ${paragraph}=    Get Element    ${xml}    body/sec/p
    Elements Should Match    ${paragraph}    <p>A document <italic>with</italic> an italic word.</p>
    [Teardown]    Remove Directory    002    recursive=True

003 Basic paragraph with bold
    [Tags]    bold    002    formatting
    ${result} =    Run Process    python ../bin/meTypeset.py docx 003.docx ./003 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./003/nlm/out.xml
    ${paragraph}=    Get Element    ${xml}    body/sec/p
    Elements Should Match    ${paragraph}    <p>A document <bold>with</bold> a bold word.</p>
    [Teardown]    Remove Directory    003    recursive=True

004 Metadata merge
    [Tags]    metadata    004
    ${result} =    Run Process    python ../bin/meTypeset.py docx 002.docx ./004 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./004/nlm/out.xml
    ${journalid}=    Get Element    ${xml}    front/journal-meta/journal-id
    Elements Should Match    ${journalid}    <journal-id journal-id-type="publisher-id">The Journal Name</journal-id>
    ${issn}=    Get Element    ${xml}    front/journal-meta/issn
    Elements Should Match    ${issn}    <issn pub-type="epub">0000-000X</issn>
    ${doi}=    Get Element    ${xml}    front/article-meta/article-id[@pub-id-type="doi"]
    Elements Should Match    ${doi}    <article-id pub-id-type="doi">10.1000/123456</article-id>
    ${title}=    Get Element    ${xml}    front/article-meta/title-group/article-title
    Elements Should Match    ${title}    <article-title>A Sample Article Title</article-title>
    ${firstname}=    Get Element    ${xml}    front/article-meta/contrib-group/contrib/name/given-names
    Elements Should Match    ${firstname}    <given-names>Martin Paul</given-names>
    ${surname}=    Get Element    ${xml}    front/article-meta/contrib-group/contrib/name/surname
    Elements Should Match    ${surname}    <surname>Eve</surname>
    ${aff}=    Get Element    ${xml}    front/article-meta/aff
    Elements Should Match    ${aff}    <aff>University of Lincoln</aff>
    ${year}=    Get Element    ${xml}    front/article-meta/pub-date/year
    Elements Should Match    ${year}    <year>2013</year>
    ${volume}=    Get Element    ${xml}    front/article-meta/volume
    Elements Should Match    ${volume}    <volume>1</volume>
    ${issue}=    Get Element    ${xml}    front/article-meta/issue
    Elements Should Match    ${issue}    <issue>1</issue>
    ${copyright}=    Get Element    ${xml}    front/article-meta/permissions/copyright-statement
    Elements Should Match    ${copyright}    <copyright-statement>Copyright © 2013, Martin Paul Eve</copyright-statement>
    ${license}=    Get Element    ${xml}    front/article-meta/permissions/license/license-p
    Elements Should Match    ${license}    <license-p>This is an open-access article distributed under the terms of the Creative Commons Attribution License, which permits unrestricted use, distribution, and reproduction in any medium, provided the original work is properly cited. The citation of this article must include: the name(s) of the authors, the name of the journal, the full URL of the article (in a hyperlinked format if distributed online) and the DOI number of the article.</license-p>
    ${uri}=    Get Element    ${xml}    front/article-meta/self-uri
    Elements Should Match    ${uri}    <self-uri>https://www.martineve.com/</self-uri>
    ${abstract}=    Get Element    ${xml}    front/article-meta/abstract/p
    Elements Should Match    ${abstract}    <p>This is a sample abstract that forms part of the metadataSample.xml file in meTypeset.</p>
    [Teardown]    Remove Directory    004    recursive=True

005 Title, non-title, title (from bolded text)
    [Tags]    formatting    bold    title    005
    ${result} =    Run Process    python ../bin/meTypeset.py docx 005.docx ./005 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./005/nlm/out.xml
    ${body}=    Get Element    ${xml}    body
    Elements Should Match    ${body}    <body><sec><title>This should be treated as a title.</title><p>This shouldn't.</p><p><bold>This </bold>shouldn't.</p></sec><sec><title>This should.</title></sec></body>    normalize_whitespace=yes
    [Teardown]    Remove Directory    005    recursive=True

006 Basic footnote test
    [Tags]    formatting    footnotes    006
    ${result} =    Run Process    python ../bin/meTypeset.py docx Footnote.docx ./006 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./006/nlm/out.xml
    ${p}=    Get Element    ${xml}    body/sec/p
    Elements Should Match    ${p}    <p>A footnote test.<xref ref-type="fn" rid="bibd2e35"/></p>    normalize_whitespace=yes
    ${fng}=    Get Element    ${xml}    back/fn-group
    Elements Should Match    ${fng}    <fn-group><fn id="bibd2e35"><p>Footnote.</p></fn></fn-group>    normalize_whitespace=yes
    [Teardown]    Remove Directory    006    recursive=True

007 Complex link test
    [Tags]    formatting    complexlink    007
    ${result} =    Run Process    python ../bin/meTypeset.py docxextracted ./ComplexLink ./007 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./007/nlm/out.xml
    ${p}=    Get Element    ${xml}    body/sec/p/ext-link
    ${attr1}=    Get Element Attribute    ${p}    {http://www.w3.org/1999/xlink}href
    Should Be Equal    ${attr1}    http://www.medscape.com/content/2003/00/45/63/456300/456300_tab.html
    Should Be Equal As Strings    ${p.text}    "Table 1."
    [Teardown]    Remove Directory    007    recursive=True

008 Non-Latin text
    [Tags]    formatting    arabic    008
    ${result} =    Run Process    python ../bin/meTypeset.py docx Arabic.docx ./008 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./008/nlm/out.xml
    ${ps}=    Get Elements    ${xml}    body/sec/p
    ${p}=    Get From List    ${ps}    0
    Elements Should Match    ${p}    <p>عل بين بدفع وترك الحربية, سمّي أعلنت والمعدات فصل تم. هو العام، بالهجوم نفس, بل أطراف ايطاليا، المتاخمة لكل. اعتداء جزيرتي أي يتم, وقرى الأوروبية، و وصل. غير ثم لعملة الستار الأوربيين, للحرب السوفييتي، ربع هو, ما سحقت بتطويق تلك.</p>    normalize_whitespace=yes
    ${title}=    Get Element    ${xml}    body/sec/title
    Elements Should Match    ${title}    <title>استمرار هذا.</title>    normalize_whitespace=yes
    [Teardown]    Remove Directory    008    recursive=True

009 Dash list classifier
    [Tags]    formatting    listclassifier    009
    ${result} =    Run Process    python ../bin/meTypeset.py docx 009.docx ./009 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./009/nlm/out.xml
    ${p}=    Get Element    ${xml}    body/sec/list
    Elements Should Match    ${p}    <list><list-item><p>Item 1</p></list-item><list-item><p>Item 2</p></list-item><list-item><p>Item 3</p></list-item></list>    normalize_whitespace=yes
    [Teardown]    Remove Directory    009    recursive=True

010 Reference list classifier
    [Tags]    formatting    listclassifier    010
    ${result} =    Run Process    python ../bin/meTypeset.py docx ListReferences01.docx ./010 -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./010/nlm/out.xml
    ${p}=    Get Element    ${xml}    back/ref-list
    Elements Should Match    ${p}    <ref-list><ref>Reference 1</ref><ref>Reference 2</ref></ref-list>    normalize_whitespace=yes
    [Teardown]    Remove Directory    010    recursive=True

011 Footnote list classifier
    [Tags]    formatting    listclassifier    011
    ${result} =    Run Process    python ../bin/meTypeset.py docx FootnotesFromText.docx ./011 -s testsettings.xml -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./011/nlm/out.xml
    ${p}=    Get Element    ${xml}    body/sec
    Elements Should Match    ${p}    <sec><p>A section with a footnote.<xref ref-type="fn" rid="bibd2e35"/> More text.</p><p>A third.<xref ref-type="fn" rid="bibd2e40"/></p><title>Footnotes</title></sec>    normalize_whitespace=yes
    ${ptwo}=    Get Element    ${xml}    back/fn-group
    Elements Should Match    ${ptwo}    <fn-group><fn id="bibd2e35"><p>The footnote should link here</p></fn><fn id="bibd2e40"><p>A second footnote</p></fn></fn-group>    normalize_whitespace=yes
    [Teardown]    Remove Directory    011    recursive=True

012 Superscripted footnotes
    [Tags]    formatting    listclassifier    012
    ${result} =    Run Process    python ../bin/meTypeset.py docx SuperscriptFootnotes.docx ./012 -s testsettings.xml -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./012/nlm/out.xml
    ${secs}=    Get Elements    ${xml}    body/sec
    ${sec}=    Get From List    ${secs}    0
    Elements Should Match    ${sec}    <sec><title>Article</title><p>Line.<xref ref-type="fn" rid="bibd2e38"/>Line.<xref ref-type="fn" rid="bibd2e42"/> Line.<xref ref-type="fn" rid="bibd2e46"/> Line.<xref ref-type="fn" rid="bibd2e50"/> Line.<xref ref-type="fn" rid="bibd2e54"/> Line.<xref ref-type="fn" rid="bibd2e59"/> Line.<xref ref-type="fn" rid="bibd2e63"/> Line.<xref ref-type="fn" rid="bibd2e67"/> Line.<xref ref-type="fn" rid="bibd2e71"/> Line.<xref ref-type="fn" rid="bibd2e75"/> Line.<xref ref-type="fn" rid="bibd2e79"/></p></sec>    normalize_whitespace=yes
    ${ptwo}=    Get Element    ${xml}    back/fn-group
    Elements Should Match    ${ptwo}    <fn-group><fn id="bibd2e38"><p>Footnote number 1</p></fn><fn id="bibd2e42"><p>Footnote number 2</p></fn><fn id="bibd2e46"><p>Footnote number 3</p></fn><fn id="bibd2e50"><p>Footnote number 4</p></fn><fn id="bibd2e54"><p>Footnote number 5</p></fn><fn id="bibd2e59"><p>Footnote number 6</p></fn><fn id="bibd2e63"><p>Footnote number 7</p></fn><fn id="bibd2e67"><p>Footnote number 8</p></fn><fn id="bibd2e71"><p>Footnote number 9</p></fn><fn id="bibd2e75"><p>Footnote number 10</p></fn><fn id="bibd2e79"><p>Footnote number 11</p></fn></fn-group>    normalize_whitespace=yes
    [Teardown]    Remove Directory    012    recursive=True

013 Table parsing
    [Tags]    formatting    listclassifier    013
    ${result} =    Run Process    python ../bin/meTypeset.py docx TableClassifier1.docx ./013 -s testsettings.xml -d    shell=True
    Log    ${result.stdout}
    Log    ${result.stderr}
    ${xml}=    Parse XML    ./013/nlm/out.xml
    ${secs}=    Get Elements    ${xml}    body/sec/p
    ${title}=    Get Element    ${xml}    body/sec/table-wrap/title
    ${caption}=    Get Element    ${xml}    body/sec/table-wrap/caption/p
    ${pone}=    Get From List    ${secs}    0
    ${ptwo}=    Get From List    ${secs}    1
    ${xrefone}=    Get Element    ${pone}    xref
    ${xreftwo}=    Get Element    ${ptwo}    xref
    Should Be Equal As Strings    ${title.text}    Table 1
    Should Be Equal As Strings    ${caption.text}    Some data.
    Should Be Equal As Strings    ${xrefone.text}    Table 1
    Should Be Equal As Strings    ${xreftwo.text}    Table 1
    [Teardown]    Remove Directory    013    recursive=True
